     T 106 S                         [ начало...                                                 ]
     X 0 S                           [ для пошаговой отладки использовать Z 0 S                  ]
     T 0 S                           [ обнуляем аккум, загружаем в О-ую ячейку ноль              ]
     T 3 S                           [ загружаем в 3 ячейку ноль (самый важный счетчик)          ]
     A [len] 99 S                    [ Загружаем в аккум длину обрабатываемого массива           ] [i]
     T 1 S                           [ Записываем в ячейку 1                                     ]
     A [len] 99 S                    [ Загружаем в аккум длину обрабатываемого массива           ] [j]
     T 2 S                           [ Записываем в лчейку 2                                     ]
     A [addr] 100 S                  [ загружаем в аккум адрес 0-го элемента массива             ]
     L 0 L                           [ Cдвигаем аккум на 1 разряд влево                          ]
     A [<r1>] 51 S                   [ прибавляем код инструкции                                 ]
     T [<r1>] 51 S                   [ ставим на место инструкцию с новым адресом                ]
[f:] A [addr] 100 S             [43] [ загружаем в аккум адрес 0-го элемента массива             ]
     L 0 L                           [ сдвигаем аккум на 1 разряд влево                          ]
     A [<r2>] 57 S                   [ прибавляем код инструкции                                 ]
     T [<r2>] 57 S                   [ ставим на место инструкцию с новым адресом                ]
[loop:]A 1 S                    [47] [ загружаем счетчик необработанных элементов массива (i)    ]
       S [one] 97 S                  [ вычитаем 1                                                ]
       G [<exit>] 96 S               [ если результат меньше 0, завершаем работу                 ]
       T 1 S                         [ ставим на место значение счетчика и обнуляем аккум (i)    ]
[r1:]  A 0 S                    [51] [ загружаем в аккум значение из ячейки N                    ]
       T 0 S                         [ записываем в рабочую ячейку, обнуляем аккум               ]
[loop2]     A 2 S [j]           [53] [ загружаем в аккум значение счетчика j                     ]
            S [one] 97 S             [ вычитаем 1                                                ]
            G 77 [<end loop2>] S     [ если прошли все элементы -> на выход из цикла             ]
            T 2 S                    [ ставим на место значение счетчика и обнуляем аккум (j)    ]
[r2:]       A 0 S               [57] [ загружаем в аккум значение из ячейки M                    ] [array(j)]
            S 0 S                    [ вычитаем N-ый елемента                                    ] [array(j)-array(i)]
            G 67 [<f1>] S            [ прыжок в 67 ячейку, если N > M                            ]
            S 97 [one] S             [ вычитаем 1, чтобы проверить на равенство                  ]
            G 71 [<f2>] S            [ прыжок в 71 ячейку, если N == M                           ]
            T 5 S                    [ если N < M, очищаем аккум и идем дальше                   ]
            A 3 S                    [ \                                                         ]
            A 97 [one] S             [ |  самый важный счетчик++                                 ]
            T 3 S                    [ /                                                         ]
            E 71 [<f2>] S            [ пропускаем f1 и прыгаем в f2                              ]
[f1:]       T 5 S               [67] [ очищаем аккум                                             ]
            A 3 S                    [ \                                                         ]
            S 97 [one] S             [ |   самый важный счетчик--                                ]
            T 3 S                    [ /                                                         ]
[f2:]       T 5 S               [71] [ очищаем аккум                                             ]
            A 97 [one] S             [ загружаем в аккум 1                                       ]
            L 0 L                    [ сдвигаем на 1 разряд влево                                ]
            A 57 [<r2>] S            [ прибавляем код инструкции, исполненной на предыдущем шаге ]
            T 57 [<r2>] S            [ записываем сформированную инструкцию в память             ]
            E 53 [<loop2>] S         [ повторяем все операции; аккумулятор обнулен               ]
[end loop2:]T 5 S               [77] [ очищаем аккум                                             ]
      A 3 S                          [ загружаем значение счетчика из 3 ячейки                   ]
      G 84 [<f3>] S                  [если самый важный счетчик < 0, то -> (f3) (возврат в loop1)]
      S 97 [one] S                   [ \    иначе -> выходим (-> 96)                             ]
      G 96 [<exit>] S                [ |                                                         ]
      S 97 [one] S                   [ |                                                         ]
      G 96 [<exit>] S                [ /                                                         ]
[f3:] T 5 S                     [84] [ очищаем аккум                                             ]
      A 97 [one] S                   [ загружаем в аккум 1                                       ]
      L 0 L		             [ сдвиг на 1 разряд влево                                   ]
      A 51 [<r1>] S                  [ прибавляем код инструкции, исполненной на предыдущем шаге ]
      T 51 [<r1>] S                  [ записываем сформированную инструкцию в память             ]
      A 98  S                        [ загружаем в аккум команду с 0 адресом из строки 98        ]
      T 57 [<r2>] S                  [ ставим кк на место                                        ]
      T 3 S                          [ обнуляем самый важный счетчик                             ]
      A 99 [len] S                   [ загружаем длину массива в акум                            ]
      T 2 S                          [ записываем во 2 ячейку (loop2)                            ]
      E 43 [<f>] S                   [ возврат в цикл                                            ]
[end loop:] [exit:] O 0 S            [ конец...                                                  ]
Z 0 S                           [96] [ останов                                                   ]
[one] P 0 L                     [97] [ константа (1)                         ]
A 0 S                           [98] [ команда для копирования               ]
[len]P 2 L                      [99] [ длина массвиа (5)                     ]
[addr]P 50 L [ 101 ]           [100] [ индекс первого элемента массива (101) ]
[array]P 1 L  [3]
[array]P 0 S  [0]
[array]P 1 S  [2]
[array]P 0 L  [1]
[array]P 2 S  [4]
